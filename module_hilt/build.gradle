if (rootProject.ext.isModuleDebug) {
    apply plugin: 'com.android.application'
    apply plugin: 'com.google.dagger.hilt.android'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    namespace 'com.business.hilt'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
//        resourcePrefix "setting_"

        if (rootProject.ext.isModuleDebug) {
            applicationId "com.business.hilt"
        }
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField("boolean", "isModuleDebug", String.valueOf(rootProject.ext.isModuleDebug))
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        kapt {
            arguments {
                //ARouter
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.jvmTargetVersion
    }

    buildFeatures {
        buildConfig true
        dataBinding true
    }

    sourceSets {
        main {
            if (rootProject.ext.isModuleDebug) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'module/**'    //移除module文件夹下的资源,即 debug/AndroidManifest.xml
                }
            }
        }
    }

    if (rootProject.ext.isModuleDebug) {
        hilt {
            enableExperimentalClasspathAggregation = true
            enableAggregatingTask = false
        }
    }
}

dependencies {
    implementation project(':module_base_router')
    implementation project(':lib_common')
    kapt rootProject.ext.depsLibs.arouterCompiler

    api rootProject.ext.depsLibs.hilt_android
    kapt rootProject.ext.depsLibs.hilt_android_compiler

    implementation rootProject.ext.depsLibs.activity_ktx
    implementation rootProject.ext.depsLibs.fragment_ktx
    implementation rootProject.ext.depsLibs.lifecycle_ktx
}