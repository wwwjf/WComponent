if (rootProject.ext.isModuleDebug){
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

android {
    namespace 'com.mine'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
//        resourcePrefix "mine_"

        if (rootProject.ext.isModuleDebug){
            applicationId "com.mine"
        }
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField("boolean", "isModuleDebug", String.valueOf(rootProject.ext.isModuleDebug))
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ AROUTER_MODULE_NAME: project.getName() ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.jvmTargetVersion
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        dataBinding true
        compose true
    }
    sourceSets {
        main {
            if (rootProject.ext.isModuleDebug){
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude 'module/**'	//移除module文件夹下的资源,即 debug/AndroidManifest.xml
                }
            }
        }
    }
}

dependencies {

    implementation project(':module_business_mvp')
    implementation project(':module_setting:export_setting')

    annotationProcessor rootProject.ext.depsLibs.arouterCompiler
}