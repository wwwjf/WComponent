plugins {
    id 'com.android.application'
}

android {
    compileSdk rootProject.ext.compileSdkVersion

    namespace rootProject.ext.namespaceName

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField("boolean", "isModule", String.valueOf(rootProject.ext.isModuleDebug))
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }

    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }

    buildFeatures {
        buildConfig true
        viewBinding true
        dataBinding true
    }
}


dependencies {
    implementation project(':module_base_router')
    implementation project(':lib_common')
    implementation project(':module_setting:export_setting')

    //打包时候才需要
    if (!rootProject.ext.isModuleDebug) {
        implementation project(':module_login')
        implementation project(':module_setting')
        implementation project(':module_mine')
        implementation project(':module_splash')

    }
    annotationProcessor rootProject.ext.depsLibs.arouterCompiler
    implementation rootProject.ext.depsLibs.appcompat
}